apply plugin: 'com.google.protobuf'
apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId "com.kantoniak.greendeer"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {

    }
    configurations.all {
        // Force the same version of com.google.code.findbugs because of mismatching dependency
        // versions. See http://g.co/androidstudio/app-test-app-conflict
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.0'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'com.android.support:appcompat-v7:26.+'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:design:26.+'
    implementation 'com.google.protobuf:protobuf-java:3.4.0'
    implementation 'io.grpc:grpc-okhttp:1.7.0'
    implementation 'io.grpc:grpc-stub:1.7.0'
    implementation ('io.grpc:grpc-protobuf-lite:1.7.0') {
        exclude module: "protobuf-lite"
    }
    implementation 'javax.annotation:jsr250-api:1.0'
    testImplementation 'junit:junit:4.12'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.4.0"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.7.0'
        }
        lite {
            artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                lite {}
                grpc {
                    option 'lite'
                }
            }
        }

    }
}